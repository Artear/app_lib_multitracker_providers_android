buildscript {
    repositories {
        google()
        jcenter()
        maven { url 'https://maven.fabric.io/public' }
        maven { url "http://comscore.bintray.com/Analytics" }
        mavenCentral()
    }
    dependencies {
        classpath "com.jfrog.bintray.gradle:gradle-bintray-plugin:$bintrayVersion"
        classpath "com.github.dcendents:android-maven-gradle-plugin:$dcendentsVersion"
        classpath "org.jetbrains.dokka:dokka-android-gradle-plugin:$dokkaVersion"
    }
}

plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'kotlin-android-extensions'
    id 'kotlin-kapt'
    id 'com.github.ben-manes.versions' version '0.21.0'
}

ext {
    bintrayRepo = 'Android'
    bintrayName = 'Providers'

    publishedGroupId = 'com.artear.multitracker'
    libraryName = 'Providers'
    artifact = 'providers'

    libraryDescription = 'A set of common trackers and extension functionality of Multitracker'

    siteUrl = 'https://github.com/Artear/app_lib_multitracker_providers_android'
    gitUrl = 'https://github.com/Artear/app_lib_multitracker_providers_android.git'

    developerId = 'artear'
    developerName = 'Artear Moviles'

    licenseName = 'The Apache Software License, Version 2.0'
    licenseUrl = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
    allLicenses = ["Apache-2.0"]

}

static int getVersionCode(branch) {
    def versionCodeCommand = "git rev-list --count " + branch
    return versionCodeCommand.execute().text.trim().toInteger()
}

static def getBranchName() {
    return "git rev-parse --abbrev-ref HEAD".execute().text.trim()
}

android {
    compileSdkVersion 28
    buildToolsVersion "28.0.3"

    def branch = getBranchName()
    def vCode = 0 + getVersionCode(branch)

    defaultConfig {
        minSdkVersion 17
        targetSdkVersion 28
        versionCode vCode
        versionName libraryVersion
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        multiDexEnabled true
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}
dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    //Test
    testImplementation "junit:junit:$junitVersion"
    androidTestImplementation "androidx.test:runner:$testRunnerVersion"
    androidTestImplementation "androidx.test.espresso:espresso-core:$espressoCoreVersion"

    //Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlinVersion"

    //AndroidX
    implementation "androidx.appcompat:appcompat:$appCompatVersion"
    implementation "androidx.vectordrawable:vectordrawable:$vectorDrawableVersion"

    implementation "com.artear.multitracker:multitracker:$multiTrackerVersion"
    implementation "com.artear.tools:tools:$toolsVersion"

    //Crashlytics
    implementation("com.crashlytics.sdk.android:crashlytics:$crashlyticsVersion@aar") {
        transitive = true
    }

    //FireBase
    implementation "com.google.firebase:firebase-core:$firebaseCoreVersion"
    implementation "com.jakewharton.timber:timber:$timberVersion"

    //ComScore
    implementation "com.comscore:android-analytics:$comscoreVersion"
}

dependencyUpdates.resolutionStrategy {
    componentSelection { rules ->
        rules.all { ComponentSelection selection ->
            boolean rejected = ['alpha', 'beta', 'rc', 'cr', 'm', 'preview', 'b', 'ea'].any { qualifier ->
                selection.candidate.version ==~ /(?i).*[.-]${qualifier}[.\d-+]*/
            }
            if (rejected) {
                selection.reject('Release candidate')
            }
        }
    }
}

group = publishedGroupId
version = libraryVersion

apply from: 'publication.gradle'
apply from: 'bintray.gradle'